SVG Car Brands Scatter Plot with Logos = 

VAR _Max_Avg_Price = MAXX( ALLSELECTED( 'dim - Car Brand Logo'[Company] ), [Avg Price] ) * 1.03
VAR _Min_Avg_Price = MINX( ALLSELECTED( 'dim - Car Brand Logo'[Company] ), [Avg Price] ) * 0.97
VAR _Range_Price = _Max_Avg_Price - _Min_Avg_Price
VAR _Step_Price = _Range_Price / 5

VAR _Max_Sales_Volume = MAXX( ALLSELECTED( 'dim - Car Brand Logo'[Company] ), [Car Sales Volume] ) * 1.1
VAR _Min_Sales_Volume = MINX( ALLSELECTED( 'dim - Car Brand Logo'[Company] ), [Car Sales Volume] ) * 0.9 
VAR _Range_Quantity = _Max_Sales_Volume - _Min_Sales_Volume
VAR _Step_Quantity = _Range_Quantity / 5

VAR _table =
SELECTCOLUMNS( 
    'dim - Car Brand Logo',
    "Price", ((( [Avg Price] - _Min_Avg_Price ) / _Range_Price) * 750 ) + 50 , 
    "Quantity Sold", ((( [Car Sales Volume] - _Min_Sales_Volume ) / _Range_Quantity) * 450 ) + 50, 
    "ImageURL", 'dim - Car Brand Logo'[Image - Base64],
    "name", 'dim - Car Brand Logo'[Company] 
)

VAR _format = IF( ISFILTERED( 'Date' ), "0", "#,##0,.0K")
VAR _x = IF( ISFILTERED( 'Date' ), 30, 25)


VAR _ScatterPlotSVG =
"data:image/svg+xml;utf8, <svg width='850' height='550' xmlns='http://www.w3.org/2000/svg'>" &
    "<text x='0' y='35' font-size='22' fill='black'>Car Brands</text>" & // Title
    "<line x1='50' y1='500' x2='800' y2='500' stroke='black' stroke-width='1'/>" & // x-axis line
    "<text x='400' y='535' font-size='16' fill='black'>Avg Price</text>" & // x-axis label
    
    "<line x1='200' y1='50' x2='200' y2='500' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5' />" &
    "<line x1='350' y1='50' x2='350' y2='500' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5' />" &
    "<line x1='500' y1='50' x2='500' y2='500' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5'/>" &
    "<line x1='650' y1='50' x2='650' y2='500' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5' />" &


    "<text x='40' y='520' font-size='12' fill='black'>" &FORMAT( _Min_Avg_Price, "$#,##0,K" )& "</text>" &
    "<text x='186' y='520' font-size='12' fill='black'>" &FORMAT( _Min_Avg_Price + _Step_Price, "$#,##0,K" )& "</text>" &
    "<text x='336' y='520' font-size='12' fill='black'>" &FORMAT( _Min_Avg_Price + (_Step_Price * 2 ), "$#,##0,K" )& "</text>" &
    "<text x='486' y='520' font-size='12' fill='black'>" &FORMAT( _Min_Avg_Price + (_Step_Price * 3 ), "$#,##0,K" )& "</text>" &
    "<text x='636' y='520' font-size='12' fill='black'>" & FORMAT( _Min_Avg_Price + (_Step_Price * 4 ), "$#,##0,K" )& "</text>" &
    "<text x='786' y='520' font-size='12' fill='black'>" & FORMAT( _Max_Avg_Price, "$#,##0,K" )& "</text>" &



    "<line x1='50' y1='140' x2='800' y2='140' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5' />" &
    "<line x1='50' y1='230' x2='800' y2='230' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5' />" &
    "<line x1='50' y1='320' x2='800' y2='320' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5' />" &
    "<line x1='50' y1='410' x2='800' y2='410' stroke='#E6E6E6' stroke-width='1' stroke-dasharray='5,5' />" &

    "<text x='"&_x&"' y='503' font-size='12' fill='black'>" &FORMAT( _Min_Sales_Volume, "#,##0" )& "</text>" &
    "<text x='"&_x&"' y='413' font-size='12' fill='black'>" &FORMAT( _Min_Sales_Volume + (_Step_Quantity ), _format )& "</text>" &
    "<text x='"&_x&"' y='323' font-size='12' fill='black'>" &FORMAT( _Min_Sales_Volume + (_Step_Quantity * 2 ), _format )& "</text>" &
    "<text x='"&_x&"' y='233' font-size='12' fill='black'>" &FORMAT( _Min_Sales_Volume + (_Step_Quantity * 3 ), _format )& "</text>" &
    "<text x='"&_x&"' y='143' font-size='12' fill='black'>" &FORMAT( _Min_Sales_Volume + (_Step_Quantity * 4 ), _format )& "</text>" &
    "<text x='"&_x&"' y='52' font-size='12' fill='black'>" &FORMAT( _Max_Sales_Volume, _format )& "</text>" &
    

    "<line x1='50' y1='50' x2='50' y2='500' stroke='black' stroke-width='1'/>" & // y-axis line
    "<text x='2.5' y='300' font-size='16' transform='rotate(-90 20,300)' fill='black'>Quantity Sold</text>" & // y-axis label

    CONCATENATEX(
        _table,
        "<image  
        href='"&[ImageURL]&"'
        width='70' 
        height='70'
        x='"& [Price] &"' 
        y='"& (500 - [Quantity Sold] ) &"'
        />"
    ) &
"</svg>"    

RETURN 
    _ScatterPlotSVG
